buildscript {
	ext {
		protobufVersion = '3.19.1'
		protobufPluginVersion = '0.8.18'
		grpcVersion = '1.51.3'
	}
}

plugins {
	id 'java'
	id 'idea'
	id 'org.springframework.boot' version '2.7.0'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'com.google.protobuf' version "${protobufPluginVersion}"
}

group = 'ru.tinkoff'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

springBoot {
	buildInfo()
}

ext {
	set('testcontainersVersion', "1.17.6")
}

dependencies {
	implementation "io.grpc:grpc-protobuf:${grpcVersion}"
	implementation "io.grpc:grpc-stub:${grpcVersion}"
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'io.micrometer:micrometer-registry-prometheus:1.10.3'
	implementation 'net.devh:grpc-server-spring-boot-starter:2.14.0.RELEASE'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:postgresql'
	testImplementation 'io.grpc:grpc-testing:1.53.0'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
	compileOnly 'jakarta.annotation:jakarta.annotation-api:1.3.5'
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:${protobufVersion}"
	}
	generatedFilesBaseDir = "$projectDir/src/generated"
	clean {
		delete generatedFilesBaseDir
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {}
		}
	}
}

dependencyManagement {
	imports {
		mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

// Optional
idea {
	module {
		sourceDirs += file("src/generated/main/java")
		sourceDirs += file("src/generated/main/grpc")
		generatedSourceDirs += file("src/generated/main/java")
		generatedSourceDirs += file("src/generated/main/grpc")
	}
}